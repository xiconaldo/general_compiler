<PROGRAM> program IDENTIFIER ; <VAR_DECL> <SUBPROG_DECLS> <COMP_COMMAND> .
<VAR_DECL> var <VAR_DECL_LIST>
<VAR_DECL>
<VAR_DECL_LIST> <ID_LIST> : <TYPE> ; <VAR_DECL_LIST*>
<VAR_DECL_LIST*> <ID_LIST> : <TYPE> ; <VAR_DECL_LIST*>
<VAR_DECL_LIST*>
<ID_LIST> IDENTIFIER <ID_LIST*>
<ID_LIST*> , IDENTIFIER <ID_LIST*>
<ID_LIST*>
<TYPE> integer
<TYPE> real
<TYPE> boolean
<SUBPROG_DECLS> <SUBPROG_DECLS*>
<SUBPROG_DECLS*> <SUBPROG_DECL> ; <SUBPROG_DECLS*>
<SUBPROG_DECLS*>
<SUBPROG_DECL> procedure IDENTIFIER <ARGS> ; <VAR_DECL> <SUBPROG_DECLS> <COMP_COMMAND>
<ARGS> ( <PARAM_LIST> )
<ARGS>
<PARAM_LIST> <ID_LIST> : <TYPE> <PARAM_LIST*>
<PARAM_LIST*> ; <ID_LIST> : <TYPE> <PARAM_LIST*>
<PARAM_LIST*>
<COMP_COMMAND> begin <OPT_COMMAND> end
<OPT_COMMAND> <COMMAND_LIST>
<OPT_COMMAND>
<COMMAND_LIST> <COMMAND> <COMMAND_LIST*>
<COMMAND_LIST*> ; <COMMAND> <COMMAND_LIST*>
<COMMAND_LIST*>
<COMMAND> IDENTIFIER <COMMAND*>
<COMMAND> <COMP_COMMAND>
<COMMAND> if <EXPR> then <COMMAND> <ELSE>
<COMMAND> while <EXPR> do <COMMAND>
<COMMAND> for IDENTIFIER ASSIGNMENT <EXPR> to <EXPR> do <COMMAND>
<COMMAND*> := <EXPR>
<COMMAND*> <PROC_ACTIV*>
<ELSE> else <COMMAND>
<ELSE>
<PROC_ACTIV*> ( <EXPR_LIST> )
<PROC_ACTIV*>
<EXPR_LIST> <EXPR> <EXPR_LIST*>
<EXPR_LIST*> , <EXPR> <EXPR_LIST*>
<EXPR_LIST*>
<EXPR> <SIMPLE_EXPR> <EXPR*>
<EXPR*> REL_OPERATOR <SIMPLE_EXPR>
<EXPR*>
<SIMPLE_EXPR> <TERM> <SIMPLE_EXPR*>
<SIMPLE_EXPR> <SIGNAL> <TERM> <SIMPLE_EXPR*>
<SIMPLE_EXPR*> ADD_OPERATOR <TERM> <SIMPLE_EXPR*>
<SIMPLE_EXPR*>
<TERM> <FACTOR> <TERM*>
<TERM*> MUL_OPERATOR <FACTOR> <TERM*>
<TERM*>
<FACTOR> IDENTIFIER <FACTOR*>
<FACTOR> INTEGER_NUMBER
<FACTOR> REAL_NUMBER
<FACTOR> true
<FACTOR> false
<FACTOR> ( <EXPR> )
<FACTOR> not <FACTOR>
<FACTOR*> ( <EXPR_LIST> )
<FACTOR*>
<SIGNAL> +
<SIGNAL> -
